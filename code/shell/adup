#!/bin/zsh
# Simple /etc/hosts updating script while merging with your own list.
# NOTE: Just so you understand how this works; the first time you run
# the script it will copy your current /etc/hosts to a file defined
# in personalhosts=, this file will not be overwritten again unless you
# do it manually. If you use the --build option it will fetch newest
# hosts file from someonewhocares.org and then merge it with your list.
# This way your /etc/hosts will be from someonewhocares.org but have
# your localhost (and any other hosts you originally had) defined too.
# If you want to add hosts to /etc/hosts, add them to $personalhosts
# then use either the --build or --refresh option to update changes.

# Write your hosts to this file, it will be merged with master list.
personalhosts=/etc/hosts.mine

help() {
<< EOF
OPTIONS:
-b|--build   Build new /etc/hosts from latest source at:
             http://www.someonewhocares.org/hosts/
             Also merging your own $personalhosts file.
-r|--refresh Only refresh /etc/hosts with changes made from
             your $personalhosts file.
-h|--help    Show this message and exit.
EOF
}

do_safe() {
  [[ -f $personalhosts ]] || cp -f /etc/hosts $personalhosts
  cp -f /etc/hosts /etc/hosts~
}

case $1 in
  '-b'|'--build')
  do_safe
  curl -s http://someonewhocares.org/hosts/hosts|\
    sed -e '1,/#<localhost>/c#<localhost>' -e '/# Acknowledgements/,$s/.*//;/^$/d' \
     -e '/#<shortcut-examples>/,/#<\/shortcut-examples>/ d' >! /etc/hosts
    <$personalhosts>> /etc/hosts
  ;;

  '-r'|'--refresh')
  do_safe
  <$personalhosts</etc/hosts~|awk ' !x[$0]--'>! /etc/hosts
  ;;

  '-h'|'--help') help
  ;;

  * ) print "Error: unrecognized option \"$1\". See --help."
  ;;
esac

